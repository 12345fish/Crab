/*========================================
  这是一个条件判断语句演示程序
  有关表达式部分，请参阅 ExpressionDemo
  
  IF 语句格式
  
  第一种:
  if 表达式
  {
    语句列表;
  }
  
  第二种:
  if 表达式
  {
    语句列表;
  }
  else
  {
    语句列表;
  }
  
  Create Date: 2014-08-26 08:02
========================================*/
main
{
   var a,b,c;
   
   //这是一个简单的IF语句格式
   if (a == 1) //如果 a 等於 1 则执行以下语句
   {
      b = 1;
   }

   //这是一个否选的格式
   if (a == 2) //如果 a 等於2 则执行以下语句
     c = 1;
   else    //否则的话，则执行下以语句
     c = 2;

   if (a) //如果判断表达式是一个简单的变量，则该语句相当於 (a == null) || (a == 0) || (a == '')
              //也就是当a为空，或a等於0，或a等於空字符串的时候
     c = 1;
   else
     c = 2;

   //这是一个多重判断的格式
   if (a == 1)        //如果 a 等於 1 则执行以下语句
     c = 1;
   else if (a == 2)    //否则如果 a 等於2 则执行以下语句
     c = 2;
   else                  //如果上面都不是的话，则执行以下语句
     c = 0;

   //这是一个IF里面嵌套IF的格式
   if (a)
   {
     if (b)
       c = 1;
     else
       c = 2;
   }
   else
   {
     c = 3;
   }
   
   //if 可以使用in表达式, 如果用in表达式, 则不能和其它表达式混合使用
   
   if (a in [1,2,3,6,7,8])   // 如果a的值 在(1,2,3,6,7,8)这个列表之中，则为true，否则为false;
                             // 相当於 if (a == 1) || (a == 2) || (a = 6) ...
   {
     a = 1;
   }
   
   if (a in [11,22..33,23])
   {
     a = 1;
   }
   else
   {
     a = 2;
   }
   
   //其它的in样式
   //if (a in [b..c])                            //如果 a 在 b 与 c之间， 其表达式相当於  (a >= b) && (a <= c)
   //if (a in [10..15])                          //相当於 (a >= 10) && (a <= 15)
   //if (a in [$"2012-01-01" .. $"2012-02-28"])  //相当於 (a >= $"2012-01-01") && (a <= $"2012-02-28")
   //if (a in [1,2, 5..8])
   
   //多重条件转移语句
   //switch (表达式)
   //case 常量1, ...
   //case 常量范围
   //case 同样也可以使用和if一样的in表达式样式
   //default   缺省。 default不是必须的。
   //case 里面的break不是必须的
   
   switch (1+(a++)+b+c)  //可以是任意表达式
   {
     case 1:           { b = 1;}  //case 值，这里是单一值
     case "Hello":     { b = 2;}  //也可以是字符串常量
     case 3..6:        { b = 3;}  //也可以是常量范围值
     case 7,8, 11..20: { b = 4;}  //还可以是组合
     default:                     //缺省情况
       {b = 0;}
   }
   
}

